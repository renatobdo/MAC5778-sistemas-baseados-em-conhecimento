PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://www.semanticweb.org/dinad/ontologies/2025/10/projeto_filmes#>

1. 
SELECT ?titulo
WHERE {
  BIND(:diretor_sofia_coppola AS ?D) 
  ?f :temDiretor ?D ;
     :titulo ?titulo .
}
ORDER BY STR(?titulo)


2.
SELECT DISTINCT ?firstName ?familyName ?personagem
WHERE {
  BIND("Lost in Translation" AS ?Ft) 
  ?f a :Filme ; :titulo ?Ft .

  { ?p :atuaEm ?f } UNION { ?f :temElenco ?p }
  ?p foaf:firstName ?firstName ; foaf:familyName ?familyName .

  OPTIONAL {
    ?ch a :Personagem ; :interpretadoPor ?p ; :apareceEm ?f ;
        :nomePersonagem ?personagem .
  }
}
ORDER BY STR(?firstName) STR(?familyName)

3.
SELECT DISTINCT ?titulo (STR(?ano_str) AS ?ano) ?dirFirstName ?dirFamilyName
WHERE {
  ?X a :Ator ;
     foaf:firstName  "Al" ;
     foaf:familyName "Pacino" ;
     :atuaEm ?f .

  ?Y a :Atriz ;
     foaf:firstName  "Diane" ;
     foaf:familyName "Keaton" ;
     :atuaEm ?f .

  ?f a :Filme ;
     :titulo        ?titulo ;
     :anoLancamento ?ano_str ;
     :temDiretor    ?dir .

  ?dir a :Diretor ;
       foaf:firstName  ?dirFirstName ;
       foaf:familyName ?dirFamilyName .
}
ORDER BY DESC(?ano_str) STR(?titulo)

4.
SELECT DISTINCT ?titulo (STR(?duracao_str) AS ?duracao)
WHERE {
  BIND(:filme_the_godfather AS ?F)
  ?F a :Filme ;
     :temDiretor ?D .

  BIND(:p_al_pacino    AS ?X)
  BIND(:p_diane_keaton AS ?Y)
  ?f a :Filme ;
     :temDiretor     ?D ;
     :titulo         ?titulo ;
     :duracaoMinutos ?duracao_str .
  {
    ?X :atuaEm ?f .
  }
  UNION
  {
    ?Y :atuaEm ?f .
  }
}
ORDER BY ?duracao_str STR(?titulo)

5.
SELECT DISTINCT ?Pessoa ?FilmeAt ?FilmeDir
WHERE {
  # Pessoa que atua em um filme (ator/atriz)
  ?Pessoa a ?tipoA ;
          :atuaEm ?FilmeAt ;
          foaf:firstName  ?firstName ;
          foaf:familyName ?familyName .
  VALUES ?tipoA { :Ator :Atriz }  

  # Mesma combinação de nome/sobrenome aparecendo como diretor de outro filme
  ?FilmeDir :temDiretor ?dir .
  ?dir a :Diretor ;
       foaf:firstName  ?firstName ;
       foaf:familyName ?familyName .
}
ORDER BY ?Pessoa ?FilmeAt ?FilmeDir

6.
SELECT DISTINCT ?dirFirstName ?dirFamilyName ?titulo (STR(?ano_str) AS ?ano)
WHERE {
  BIND(2003 AS ?N1)
  BIND(2008 AS ?N2)

  BIND(:p_scarlett_johansson AS ?X)
  BIND(:p_tom_wilkinson      AS ?Y)

  ?f a :Filme ;
     :titulo        ?titulo ;
     :anoLancamento ?ano_str ;
     :temDiretor    ?dir .

  FILTER(?ano_str >= ?N1 && ?ano_str <= ?N2)

  ?X :atuaEm ?f .
  ?Y :atuaEm ?f .

  ?dir a :Diretor ;
       foaf:firstName  ?dirFirstName ;
       foaf:familyName ?dirFamilyName .
}
ORDER BY ?ano_str STR(?titulo)

7.
SELECT DISTINCT
       ?atrizFirst ?atrizFamily
       ?atorFirst  ?atorFamily
WHERE {
  BIND(90  AS ?M1)
  BIND(120 AS ?M2)

  ?filme a :Filme ;
         :duracaoMinutos ?duracao .

  FILTER(?duracao >= ?M1 && ?duracao <= ?M2)

  ?atriz a :Atriz ;
         :atuaEm ?filme ;
         foaf:firstName  ?atrizFirst ;
         foaf:familyName ?atrizFamily .

  ?ator  a :Ator ;
         :atuaEm ?filme ;
         foaf:firstName  ?atorFirst ;
         foaf:familyName ?atorFamily .
}
ORDER BY STR(?atrizFirst) STR(?atrizFamily) STR(?atorFirst) STR(?atorFamily)

8.
SELECT ?dirFirstName ?dirFamilyName (COUNT(DISTINCT ?f) AS ?numFilmes)
WHERE {
  ?ator a :Ator ;
        foaf:firstName  "John" ;
        foaf:familyName "Goodman" ;
        :atuaEm ?f .

  ?f a :Filme ;
     :temDiretor ?dir .

  ?dir a :Diretor ;
       foaf:firstName  ?dirFirstName ;
       foaf:familyName ?dirFamilyName .
}
GROUP BY ?dir ?dirFirstName ?dirFamilyName
ORDER BY DESC(?numFilmes)
LIMIT 1

9.
SELECT ?titulo (STR(?ano_str) AS ?ano) ?nomePersonagem
WHERE {
  {
    SELECT (MIN(?ano0) AS ?anoMin)
    WHERE {
      ?pers0 a :Personagem ;
             :interpretadoPor :p_gillian_welch ;
             :apareceEm ?f0 .
      ?f0 a :Filme ;
          :anoLancamento ?ano0 .
    }
  }

  ?pers a :Personagem ;
        :interpretadoPor :p_gillian_welch ;
        :apareceEm ?f ;
        :nomePersonagem ?nomePersonagem .

  ?f a :Filme ;
     :titulo        ?titulo ;
     :anoLancamento ?ano_str .

  FILTER(?ano_str = ?anoMin)
}
ORDER BY STR(?titulo)

10.
SELECT ?titulo (STR(?duracao_str) AS ?duracao)
WHERE {
  ?f a :Filme ;
     :temDiretor ?D ;
     :titulo ?titulo ;
     :duracaoMinutos ?duracao_str .
  {
    SELECT ?D (MAX(?duracao2) AS ?durMax)
    WHERE {
      ?f2 a :Filme ;
          :temDiretor ?D ;
          :duracaoMinutos ?duracao2 .
    }
    GROUP BY ?D
  }
  FILTER (?duracao_str = ?durMax)
  FILTER (?D = :diretor_sofia_coppola)
}
ORDER BY STR(?titulo)
