PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://www.semanticweb.org/dinad/ontologies/2025/10/projeto_filmes#>

1. 
SELECT ?TituloDoFilme
WHERE {
  BIND(:diretor_sofia_coppola AS ?D) 
  ?f :temDiretor ?D ;
     :titulo ?TituloDoFilme .
}
ORDER BY STR(?TituloDoFilme)


2.
SELECT DISTINCT ?PrimeiroNome ?UltimoNome ?Personagem
WHERE {
  BIND("Lost in Translation" AS ?Ft) 
  ?f a :Filme ; :titulo ?Ft .

  { ?p :atuaEm ?f } UNION { ?f :temElenco ?p }
  ?p foaf:firstName ?PrimeiroNome ; foaf:familyName ?UltimoNome .

  OPTIONAL {
    ?ch a :Personagem ; :interpretadoPor ?p ; :apareceEm ?f ;
        :nomePersonagem ?Personagem .
  }
}
ORDER BY STR(?PrimeiroNome) STR(?UltimoNome)

3.
SELECT DISTINCT ?Filme ?Diretor (STR(?anoLanc) AS ?Ano) 
WHERE {
  BIND(:p_al_pacino AS ?X)
  BIND(:p_diane_keaton AS ?Y)

  ?X :atuaEm ?f .
  ?Y :atuaEm ?f .

  ?f a :Filme ;
     :titulo ?titulo ;
     :anoLancamento ?anoLanc ;
     :temDiretor ?Diretor .

  BIND(?f AS ?Filme)
}
ORDER BY DESC(?anoLanc) STR(?Filme)

4.
SELECT DISTINCT ?Filme (STR(?duracao_str) AS ?Duracao)
WHERE {
  BIND(:filme_the_godfather AS ?F)
  ?F a :Filme ;
     :temDiretor ?D .

  BIND(:p_al_pacino    AS ?X)
  BIND(:p_diane_keaton AS ?Y)

  ?f a :Filme ;
     :temDiretor     ?D ;
     :duracaoMinutos ?duracao_str .

  {
    ?X :atuaEm ?f .
  }
  UNION
  {
    ?Y :atuaEm ?f .
  }

  BIND(?f AS ?Filme)
}
ORDER BY ?duracao_str STR(?Filme)

5.
SELECT DISTINCT ?Pessoa ?FilmeAt ?FilmeDir
WHERE {
  ?Pessoa a ?tipoA ;
          :atuaEm ?FilmeAt ;
          foaf:firstName  ?firstName ;
          foaf:familyName ?familyName .
  VALUES ?tipoA { :Ator :Atriz }  

  ?FilmeDir :temDiretor ?dir .
  ?dir a :Diretor ;
       foaf:firstName  ?firstName ;
       foaf:familyName ?familyName .
}
ORDER BY ?Pessoa ?FilmeAt ?FilmeDir

6.
SELECT DISTINCT (STR(?ano) AS ?Ano) ?Diretor ?Filme
WHERE {
  BIND(2003 AS ?N1)
  BIND(2008 AS ?N2)

  BIND(:p_scarlett_johansson AS ?X)
  BIND(:p_tom_wilkinson      AS ?Y)

  ?f a :Filme ;
     :titulo ?titulo ;
     :anoLancamento ?ano ;
     :temDiretor ?Diretor .

  FILTER(?ano >= ?N1 && ?ano <= ?N2)

  ?X :atuaEm ?f .
  ?Y :atuaEm ?f .

  BIND(?f AS ?Filme)
}
ORDER BY ?Ano STR(?Filme)

7.
SELECT DISTINCT
       ?Atriz
       ?Ator
       ?Filme
       (STR(?duracao) AS ?Duracao)
WHERE {
  BIND(90  AS ?M1)
  BIND(120 AS ?M2)

  ?f a :Filme ;
     :titulo ?titulo ;
     :duracaoMinutos ?duracao .

  FILTER(?duracao >= ?M1 && ?duracao <= ?M2)

  ?atriz a :Atriz ;
         :atuaEm ?f .

  ?ator a :Ator ;
        :atuaEm ?f .

  BIND(?f AS ?Filme)
  BIND(?atriz AS ?Atriz)
  BIND(?ator AS ?Ator)
}
ORDER BY
  STR(?Atriz)
  STR(?Ator)
  STR(?Filme)

8.
SELECT ?Diretor
WHERE {
  {
    SELECT ?dir (COUNT(DISTINCT ?f) AS ?numFilmes)
    WHERE {
      ?ator a :Ator ;
            foaf:firstName  "John" ;
            foaf:familyName "Goodman" ;
            :atuaEm ?f .

      ?f a :Filme ;
         :temDiretor ?dir .
    }
    GROUP BY ?dir
  }

  {
    SELECT (MAX(?numFilmes) AS ?numMax)
    WHERE {
      SELECT ?dir (COUNT(DISTINCT ?f) AS ?numFilmes)
      WHERE {
        ?ator a :Ator ;
              foaf:firstName  "John" ;
              foaf:familyName "Goodman" ;
              :atuaEm ?f .
        ?f :temDiretor ?dir .
      }
      GROUP BY ?dir
    }
  }

  FILTER(?numFilmes = ?numMax)

  BIND(?dir AS ?Diretor)
}

9.
SELECT ?Filme (STR(?ano_str) AS ?Ano) ?Personagem
WHERE {
  {
    SELECT (MIN(?ano0) AS ?anoMin)
    WHERE {
      ?pers0 a :Personagem ;
             :interpretadoPor :p_gillian_welch ;
             :apareceEm ?f0 .
      ?f0 a :Filme ;
          :anoLancamento ?ano0 .
    }
  }

  ?pers a :Personagem ;
        :interpretadoPor :p_gillian_welch ;
        :apareceEm ?f ;
        :nomePersonagem ?Personagem .

  ?f a :Filme ;
     :titulo        ?titulo ;
     :anoLancamento ?ano_str .

  FILTER(?ano_str = ?anoMin)

  BIND(?f AS ?Filme)
}
ORDER BY STR(?Filme)

10.
SELECT ?Filme (STR(?duracao_str) AS ?Duracao)
WHERE {
  ?f a :Filme ;
     :temDiretor ?D ;
     :titulo ?Filme ;
     :duracaoMinutos ?duracao_str .
  {
    SELECT ?D (MAX(?duracao2) AS ?durMax)
    WHERE {
      ?f2 a :Filme ;
          :temDiretor ?D ;
          :duracaoMinutos ?duracao2 .
    }
    GROUP BY ?D
  }
  FILTER (?duracao_str = ?durMax)
  FILTER (?D = :diretor_sofia_coppola)
}
ORDER BY STR(?titulo)
